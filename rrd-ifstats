#!/usr/bin/perl

use v5.10;
use warnings;
use strict;
use experimental 'postderef';

use RRDTool::OO;
use FindBin qw($Bin);
use Path::Tiny;
use POSIX qw(strftime);
use Template;

#==============================================================================
#=== FUNCTIONS ================================================================
#==============================================================================

sub get_location
{
  my $dir = shift;
  my $path = path($Bin);

  # return default path if no argument
  return $path if !$dir;

  # get absolute/relative path if argument is specified
  if(path($dir)->is_absolute) {
    $path = path($dir);
  } else {
    $path = $path->child($dir);
  }

  # ensure the resulting path's existence
  if(!$path->exists) {
    die "Directory " . $path . " does not exist";
  }

  # finish
  return $path;
}


#==============================================================================
#=== MAIN =====================================================================
#==============================================================================

# read configuration
my $cfg_file = path "$Bin/rrd-ifstats.conf";
my %cfg = eval($cfg_file->slurp);
die $@ unless %cfg;

# read interface counts
open(my $fh, '/proc/net/dev') or die 'Failed to open /proc/net/dev';
chomp(my @net_dev = <$fh>);
close($fh);
splice(@net_dev, 0, 2);

# parse interface counts
my %ifs;
foreach (@net_dev) {
  my @l = split(/[\s:]+/);
  $ifs{$l[0]} = {
    tx => {
      bytes => $l[9],
      packets => $l[10],
    },
    rx => {
      bytes => $l[1],
      packets => $l[2],
    },
  };
}

# get hostname
open(F, '-|', 'hostname') || die 'Failed to get hostname';
chomp(my $hostname = <F>);
close(F);

#--- iterate over interfaces specified on command-line

my $rrdpath = get_location($cfg{rrddir});

foreach my $if (@ARGV) {

  # skip unknown interface
  next if !exists $ifs{$if};

  # get rrd database file
  my $rrdfile = $rrdpath->child("$if.rrd");
  my $rrd = RRDTool::OO->new(
    file => $rrdfile->canonpath
  );

  # create a new RRD database if it does not exist
  $rrd->create($cfg{rrd_db}->@*) unless $rrdfile->exists;

  $rrd->update(
    values => {
      tx_bytes => $ifs{$if}{tx}{bytes},
      rx_bytes => $ifs{$if}{rx}{bytes},
      tx_pkts => $ifs{$if}{tx}{packets},
      rx_pkts => $ifs{$if}{rx}{packets}
    }
  );

  #--- create graphs

  my $dst = get_location($cfg{dstdir});

  my $tt = Template->new({
    ABSOLUTE => 1,
    ENCODING => 'utf8',
  });

  my $graph_factory = sub {
    my ($end, $suffix) = @_;

    return $rrd->graph(
      image => $dst->child("${if}-${suffix}.png")->canonpath,
      start => (time() - $end),
      end => time(),
      lower_limit => 0,
      width => $cfg{width},
      height => $cfg{height},

      # sources

      draw => {
        type => 'hidden',
        dsname => 'rx_bytes',
        name => 'rx_bytes'
      },
      draw => {
        type => 'hidden',
        dsname => 'tx_bytes',
        name => 'tx_bytes'
      },

      # convert data from bytes to bits

      draw => {
        type => 'hidden',
        name => 'rx_bits',
        cdef => 'rx_bytes,8,*'
      },
      draw => {
        type => 'hidden',
        name => 'tx_bits',
        cdef => 'tx_bytes,8,*'
      },

      # record last values

      draw => {
        type => 'hidden',
        name => 'rx_last',
        vdef => 'rx_bits,LAST'
      },
      draw => {
        type => 'hidden',
        name => 'tx_last',
        vdef => 'tx_bits,LAST'
      },

      # get average

      draw => {
        type => 'hidden',
        name => 'rx_avg',
        vdef => 'rx_bits,AVERAGE'
      },
      draw => {
        type => 'hidden',
        name => 'tx_avg',
        vdef => 'tx_bits,AVERAGE'
      },

      # get maxima

      draw => {
        type => 'hidden',
        name => 'rx_max',
        vdef => 'rx_bits,MAXIMUM'
      },
      draw => {
        type => 'hidden',
        name => 'tx_max',
        vdef => 'tx_bits,MAXIMUM'
      },

      # graw graph
      draw => {
        type => 'area',
        color => '00CC00',
        cdef => 'rx_bits',
        legend => 'Incoming traffic'
      },
      gprint => {
        draw => 'rx_last',
        format => '\tCur %3.1lf %sbps'
      },
      gprint => {
        draw => 'rx_avg',
        format => '\tAvg %3.1lf %sbps'
      },
      gprint => {
        draw => 'rx_max',
        format => '\tMax %3.1lf %sbps\n'
      },

      draw => {
        type => 'line',
        thickness => 1,
        color => '0000CC',
        cdef => 'tx_bits',
        legend => 'Outgoing traffic'
      },
      gprint => {
        draw => 'tx_last',
        format => '\tCur %3.1lf %sbps'
      },
      gprint => {
        draw => 'tx_avg',
        format => '\tAvg %3.1lf %sbps'
      },
      gprint => {
        draw => 'tx_max',
        format => '\tMax %3.1lf %sbps\n'
      },
    );
  };

  $graph_factory->(3600*24, 'day');
  $graph_factory->(3600*24*7, 'week');
  $graph_factory->(3600*24*30, 'month');
  $graph_factory->(3600*24*365, 'year');

  #--- create html file

  $tt->process(
    "$Bin/index.tt",
    {
      gentime => strftime('%c', localtime()),
      ifname => $if,
      hostname => $hostname
    },
    $cfg{dstdir} . "/$if.html",
    { binmode => ':utf8' }
  ) || die $tt->error(), "\n"

}
